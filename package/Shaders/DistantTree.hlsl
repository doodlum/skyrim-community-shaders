#include "Common/Color.hlsl"
#include "Common/FrameBuffer.hlsl"
#include "Common/LightingData.hlsl"
#include "Common/MotionBlur.hlsl"

#ifdef VR
cbuffer VRValues : register(b13)
{
	float AlphaTestRefRS : packoffset(c0);
	float StereoEnabled : packoffset(c0.y);
	float2 EyeOffsetScale : packoffset(c0.z);
	float4 EyeClipEdge[2] : packoffset(c1);
}

const static float4x4 M_IdentityMatrix = {
	{ 1, 0, 0, 0 }, { 0, 1, 0, 0 }, { 0, 0, 1, 0 }, { 0, 0, 0, 1 }
};
#endif  // VR

struct VS_INPUT
{
	float3 Position : POSITION0;
	float2 TexCoord0 : TEXCOORD0;
	float4 InstanceData1 : TEXCOORD4;
	float4 InstanceData2 : TEXCOORD5;
	float4 InstanceData3 : TEXCOORD6;  // Unused
	float4 InstanceData4 : TEXCOORD7;  // Unused
#if defined(VR)
	uint InstanceID : SV_INSTANCEID;
#endif  // VR
};

struct VS_OUTPUT
{
	float4 Position : SV_POSITION0;
	float3 TexCoord : TEXCOORD0;
#if defined(RENDER_DEPTH)
	float4 Depth : TEXCOORD3;
#else
	float4 WorldPosition : POSITION1;
	float4 PreviousWorldPosition : POSITION2;
#endif  // RENDER_DEPTH
	float3 SphereNormal : TEXCOORD4;

#if !defined(VR)
	row_major float3x4 World[1] : POSITION3;
#else
	row_major float3x4 World[2] : POSITION3;
#endif  // VR
#if defined(VR)
	float ClipDistance : SV_ClipDistance0;  // o11
	float CullDistance : SV_CullDistance0;  // p11
	uint EyeIndex : EYEIDX0;
#endif  // VR
};

#ifdef VSHADER
cbuffer PerTechnique : register(b0)
{
	float4 FogParam : packoffset(c0);
};

cbuffer PerGeometry : register(b2)
{
#	if !defined(VR)
	row_major float4x4 WorldViewProj[1] : packoffset(c0);
	row_major float4x4 World[1] : packoffset(c4);
	row_major float4x4 PreviousWorld[1] : packoffset(c8);
#	else   // VR
	row_major float4x4 WorldViewProj[2] : packoffset(c0);
	row_major float4x4 World[2] : packoffset(c8);
	row_major float4x4 PreviousWorld[2] : packoffset(c16);
#	endif  // !VR
};

VS_OUTPUT main(VS_INPUT input)
{
	VS_OUTPUT vsout;
#	if !defined(VR)
	uint eyeIndex = 0;
#	else   // VR
	uint eyeIndex = StereoEnabled * (input.InstanceID.x & 1);
#	endif  // !VR

	float3 scaledModelPosition = input.InstanceData1.www * input.Position.xyz;
	float3 adjustedModelPosition = 0.0.xxx;
	adjustedModelPosition.x = dot(float2(1, -1) * input.InstanceData2.xy, scaledModelPosition.xy);
	adjustedModelPosition.y = dot(input.InstanceData2.yx, scaledModelPosition.xy);
	adjustedModelPosition.z = scaledModelPosition.z;

	float4 finalModelPosition = float4(input.InstanceData1.xyz + adjustedModelPosition.xyz, 1.0);
	float4 viewPosition = mul(WorldViewProj[eyeIndex], finalModelPosition);

#	ifdef RENDER_DEPTH
	vsout.Depth.xy = viewPosition.zw;
	vsout.Depth.zw = input.InstanceData2.zw;
#	else   // !RENDER_DEPTH
	vsout.WorldPosition = mul(World[eyeIndex], finalModelPosition);
	vsout.PreviousWorldPosition = mul(PreviousWorld[eyeIndex], finalModelPosition);
#	endif  // RENDER_DEPTH

	vsout.Position = viewPosition;
	vsout.TexCoord = float3(input.TexCoord0.xy, FogParam.z);

	scaledModelPosition = input.Position.xyz;
	adjustedModelPosition.x = dot(float2(1, -1) * input.InstanceData2.xy, scaledModelPosition.xy);
	adjustedModelPosition.y = dot(input.InstanceData2.yx, scaledModelPosition.xy);
	adjustedModelPosition.z = scaledModelPosition.z;

	vsout.SphereNormal.xyz = mul(World[eyeIndex], normalize(float4(adjustedModelPosition, 0)));

	vsout.World[0] = World[0];
#	ifdef VR
	vsout.World[1] = World[1];
	vsout.EyeIndex = eyeIndex;
#	endif  // VR

#	ifdef VR
	float4 r0;
	float4 projSpacePosition = vsout.Position;
	r0.xyzw = 0;
	if (0 < StereoEnabled) {
		r0.yz = dot(projSpacePosition, EyeClipEdge[eyeIndex]);  // projSpacePosition is clipPos
	} else {
		r0.yz = float2(1, 1);
	}

	r0.w = 2 + -StereoEnabled;
	r0.x = dot(EyeOffsetScale, M_IdentityMatrix[eyeIndex].xy);
	r0.xw = r0.xw * projSpacePosition.wx;
	r0.x = StereoEnabled * r0.x;

	vsout.Position.x = r0.w * 0.5 + r0.x;
	vsout.Position.yzw = projSpacePosition.yzw;

	vsout.ClipDistance.x = r0.z;
	vsout.CullDistance.x = r0.y;
#	endif  // VR
	return vsout;
}
#endif  // VSHADER

typedef VS_OUTPUT PS_INPUT;

struct PS_OUTPUT
{
	float4 Albedo : SV_Target0;

#if !defined(RENDER_DEPTH)
	float2 MotionVector : SV_Target1;
	float4 Normal : SV_Target2;
#endif  // !RENDER_DEPTH
};

#ifdef PSHADER
SamplerState SampDiffuse : register(s0);

Texture2D<float4> TexDiffuse : register(t0);

#	if !defined(VR)
cbuffer AlphaTestRefCB : register(b11)
{
	float AlphaTestRefRS : packoffset(c0);
}
#	endif  // !VR

cbuffer PerTechnique : register(b0)
{
	float4 DiffuseColor : packoffset(c0);
	float4 AmbientColor : packoffset(c1);
};

const static float DepthOffsets[16] = { 0.003921568, 0.533333361, 0.133333340, 0.666666687, 0.800000000, 0.266666681, 0.933333337, 0.400000000, 0.200000000, 0.733333349, 0.066666670, 0.600000000, 0.996078432, 0.466666669, 0.866666675, 0.333333343 };

#	if defined(TREE_LOD_LIGHTING)
#		include "TreeLodLightning/TreeLodLightning.hlsli"
#	endif  // TREE_LOD_LIGHTING

#	if defined(SCREEN_SPACE_SHADOWS)
#		include "ScreenSpaceShadows/ShadowsPS.hlsli"
#	endif  // SCREEN_SPACE_SHADOWS

#	if defined(CLOUD_SHADOWS)
#		include "CloudShadows/CloudShadows.hlsli"
#	endif  // CLOUD_SHADOWS

PS_OUTPUT main(PS_INPUT input, bool frontFace
			   : SV_IsFrontFace)
{
	PS_OUTPUT psout;

#	if !defined(VR)
	uint eyeIndex = 0;
#	else   // VR
	uint eyeIndex = input.EyeIndex;
#	endif  // !VR

#	if defined(RENDER_DEPTH)
	uint2 temp = uint2(input.Position.xy);
	uint index = ((temp.x << 2) & 12) | (temp.y & 3);

	float depthOffset = 0.5 - DepthOffsets[index];
	float depthModifier = (input.Depth.w * depthOffset) + input.Depth.z - 0.5;

	if (depthModifier < 0) {
		discard;
	}

	float alpha = TexDiffuse.Sample(SampDiffuse, input.TexCoord.xy).w;

	if ((alpha - AlphaTestRefRS) < 0) {
		discard;
	}

	psout.Albedo.xyz = input.Depth.xxx / input.Depth.yyy;
	psout.Albedo.w = 0;
#	else  // !RENDER_DEPTH

	float4 baseColor = TexDiffuse.Sample(SampDiffuse, input.TexCoord.xy);

#		if defined(DO_ALPHA_TEST)
	if ((baseColor.w - AlphaTestRefRS) < 0) {
		discard;
	}
#		endif  // DO_ALPHA_TEST

	float2 screenMotionVector = GetSSMotionVector(input.WorldPosition, input.PreviousWorldPosition, eyeIndex);

	psout.MotionVector = screenMotionVector;

#		if defined(TREE_LOD_LIGHTING)

	float3 ddx = ddx_coarse(input.WorldPosition);
	float3 ddy = ddy_coarse(input.WorldPosition);
	float3 normal = normalize(cross(ddx, ddy));

	float3 viewDirection = -normalize(input.WorldPosition.xyz);
	float3 worldNormal = normal;

	worldNormal = normalize(input.SphereNormal.xyz);
	worldNormal.xy *= 2;
	worldNormal = normalize(worldNormal);
	worldNormal = normalize(lerp(-worldNormal, normal, 0.25));

	if (ComplexAtlasTexture && EnableComplexTreeLOD) {
		float3 normalColor = TexDiffuse.Sample(SampDiffuse, float2(input.TexCoord.x, 0.5 + input.TexCoord.y));
		normalColor = TransformNormal(normalColor);
		// Increases the strength of the normal to simulate more advanced lighting.
		normalColor.xy *= 2;
		normalColor = normalize(normalColor);
		// world-space -> tangent-space -> world-space.
		// This is because we don't have pre-computed tangents.
		worldNormal.xyz = normalize(mul(normalColor.xyz, CalculateTBN(worldNormal.xyz, -input.WorldPosition.xyz, input.TexCoord.xy)));
	}

	float3 dirLightColor = lerp(RGBToLuminance(DirLightColor.xyz), DirLightColor.xyz, 0.5) * 0.5;

	if (EnableDirLightFix) {
		dirLightColor *= DirLightScale;
	}

#			if defined(CLOUD_SHADOWS)
	float3 normalizedDirLightDirectionWS = -normalize(mul(input.World[eyeIndex], float4(DirLightDirection.xyz, 0))).xyz;

	float3 cloudShadowMult = 1.0;
	if (perPassCloudShadow[0].EnableCloudShadows && !lightingData[0].Reflections) {
		cloudShadowMult = getCloudShadowMult(input.WorldPosition.xyz, normalizedDirLightDirectionWS.xyz, SampDiffuse);
		dirLightColor *= cloudShadowMult;
	}
#			endif  // CLOUD_SHADOWS

	float3 nsDirLightColor = dirLightColor;

#			if defined(SCREEN_SPACE_SHADOWS)
	float dirLightSShadow = PrepassScreenSpaceShadows(input.WorldPosition.xyz, eyeIndex);
#			endif  // SCREEN_SPACE_SHADOWS

	float3 diffuseColor = 0;
	float3 lightsDiffuseColor = 0;

	float dirLightAngle = dot(worldNormal.xyz, DirLightDirection.xyz);
	float3 dirDiffuseColor = dirLightColor * saturate(dirLightAngle);

	lightsDiffuseColor += dirDiffuseColor * dirLightColor;

	float3 subsurfaceColor = lerp(RGBToLuminance(baseColor.xyz), baseColor.xyz, 2.0);

	// Applies lighting across the whole surface apart from what is already lit.
	lightsDiffuseColor += subsurfaceColor * nsDirLightColor * GetSoftLightMultiplier(dirLightAngle, SubsurfaceScatteringAmount);

	// Applies lighting from the opposite direction. Does not account for normals perpendicular to the light source.
	lightsDiffuseColor += subsurfaceColor * dirLightColor * saturate(-dirLightAngle) * SubsurfaceScatteringAmount;

	float3 directionalAmbientColor = mul(DirectionalAmbient, float4(worldNormal.xyz, 1));
#			if defined(CLOUD_SHADOWS)
	if (perPassCloudShadow[0].EnableCloudShadows && !lightingData[0].Reflections)
		directionalAmbientColor *= lerp(1.0, cloudShadowMult, perPassCloudShadow[0].AbsorptionAmbient);
#			endif  // CLOUD_SHADOWS
	lightsDiffuseColor += directionalAmbientColor;

	diffuseColor += lightsDiffuseColor;

	float3 color = diffuseColor * baseColor.xyz;
#		else
	float3 color = (input.TexCoord.zzz * DiffuseColor.xyz + AmbientColor.xyz) * baseColor.xyz;
	psout.Normal = float4(0.5, 0.5, 0, 0);
#		endif  // TREE_LOD_LIGHTING

	psout.Albedo.xyz = color;
	psout.Albedo.w = 1;
#	endif      // RENDER_DEPTH

	return psout;
}
#endif  // PSHADER
